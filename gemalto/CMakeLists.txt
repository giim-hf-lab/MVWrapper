if (NOT GEMALTO_SUPERDOG_VERSION)
	set(GEMALTO_SUPERDOG_VERSION "2.5")
endif ()

if (NOT GEMALTO_SUPERDOG_BUILD)
	set(GEMALTO_SUPERDOG_BUILD "demo")
endif ()

if (NOT GEMALTO_SUPERDOG_VENDOR_CODE_LABEL)
	set(GEMALTO_SUPERDOG_VENDOR_CODE_LABEL "DEMOMA")
endif ()

if (WIN32)
	if (DEFINED ENV{PROGRAMFILES\(x86\)})
		set(PROGRAMFILES_BASE "$ENV{PROGRAMFILES\(x86\)}")
		set(GEMALTO_ARCHITECTURE "x64")
		set(GEMALTO_LIBRARY_SUFFIX "_x64")
	elseif (DEFINED ENV{PROGRAMFILES})
		set(PROGRAMFILES_BASE "$ENV{PROGRAMFILES}")
		set(GEMALTO_ARCHITECTURE "win32")
	else ()
		message(FATAL_ERROR "Unknown environment detected.")
	endif ()

	cmake_path(
		APPEND PROGRAMFILES_BASE
			"Gemalto"
			"SuperDog"
			"${GEMALTO_SUPERDOG_VERSION}"
		OUTPUT_VARIABLE GEMALTO_SUPERDOG_BASE
	)
	cmake_path(
		APPEND GEMALTO_SUPERDOG_BASE
			"API"
			"Licensing"
			"C"
			"${GEMALTO_ARCHITECTURE}"
		OUTPUT_VARIABLE GEMALTO_SUPERDOG_LICENSING_API_DIR
	)
	cmake_path(
		APPEND GEMALTO_SUPERDOG_LICENSING_API_DIR
			"libdog_windows${GEMALTO_LIBRARY_SUFFIX}_${GEMALTO_SUPERDOG_BUILD}.lib"
		OUTPUT_VARIABLE GEMALTO_SUPERDOG_LICENSING_API_LIBRARY
	)
else ()
	message(FATAL_ERROR "Gemalto wrapper not supported on ${CMAKE_HOST_SYSTEM_NAME}.")
endif ()

add_library(gemalto_superdog_api STATIC IMPORTED)
set_target_properties(gemalto_superdog_api PROPERTIES
	IMPORTED_LOCATION "${GEMALTO_SUPERDOG_LICENSING_API_LIBRARY}"
	INTERFACE_INCLUDE_DIRECTORIES
		"${GEMALTO_SUPERDOG_LICENSING_API_DIR}"
	INTERFACE_LINK_LIBRARIES
		legacy_stdio_definitions.lib
	SYSTEM TRUE
)

cmake_path(
	APPEND CMAKE_CURRENT_SOURCE_DIR
		"vendor_code.py"
	OUTPUT_VARIABLE GEMALTO_VENDOR_CODE_GENERATOR
)
cmake_path(
	APPEND GEMALTO_SUPERDOG_BASE
		"VendorCodes"
		"${GEMALTO_SUPERDOG_VENDOR_CODE_LABEL}.hvc"
	OUTPUT_VARIABLE GEMALTO_SUPERDOR_VENDOR_CODE_FILE
)
cmake_path(
	APPEND CMAKE_CURRENT_BINARY_DIR
		"vendor_codes"
	OUTPUT_VARIABLE GEMALTO_VENDOR_CODE_CPP_DIR
)
cmake_path(
	APPEND GEMALTO_VENDOR_CODE_CPP_DIR
		"superdog.vc"
	OUTPUT_VARIABLE GEMALTO_SUPERDOR_VENDOR_CODE_CPP
)
file(MAKE_DIRECTORY "${GEMALTO_VENDOR_CODE_CPP_DIR}")
add_custom_command(
	OUTPUT
		"${GEMALTO_SUPERDOR_VENDOR_CODE_CPP}"
	COMMAND
		"${Python_EXECUTABLE}"
		"${GEMALTO_VENDOR_CODE_GENERATOR}"
		"--name" "SUPERDOG"
		"--input-file" "${GEMALTO_SUPERDOR_VENDOR_CODE_FILE}"
		"--output-file" "${GEMALTO_SUPERDOR_VENDOR_CODE_CPP}"
	DEPENDS
		"${GEMALTO_SUPERDOR_VENDOR_CODE_FILE}"
		"${GEMALTO_VENDOR_CODE_GENERATOR}"
	WORKING_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}"
	COMMENT "Generating Gemalto vendor code for SuperDog"
	VERBATIM
	USES_TERMINAL
)
add_custom_target(gemalto_superdog_vendor_code
	DEPENDS
		"${GEMALTO_SUPERDOR_VENDOR_CODE_CPP}"
)

if (MSVC)
	create_msvc_cxx_module_target(gemalto
		INCLUDE_DIRECTORIES
			"${GEMALTO_VENDOR_CODE_CPP_DIR}"
		LINK_LIBRARIES
			gemalto_superdog_api
	)
	add_dependencies(gemalto gemalto_superdog_vendor_code)
	add_library(gemalto::gemalto ALIAS gemalto)
else ()
	message(FATAL_ERROR "Gemalto wrapper requires C++20 module support")
endif ()

add_executable(gemalto_driver EXCLUDE_FROM_ALL main.cpp)
target_link_libraries(gemalto_driver
	PRIVATE
		gemalto::gemalto
)
